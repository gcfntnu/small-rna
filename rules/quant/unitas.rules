#-*- mode:snakemake -*-

from os.path import join, abspath
import warnings
warnings.filterwarnings("ignore", message="numpy.dtype size changed")

include:
    join(GCFDB_DIR, 'unitas.db')


rule spikein_unitas:
    input:
        get_spikein_fasta()
    params:
        script = srcdir('scripts/spikein_unitas.py')
    output:
        join(QUANT_INTERIM, 'unitas', '_spikein_unitas_formatted.fa')
    shell:
        'python {params.script} {input} > {output}'
        
UNITAS_REFSEQ = []
if config['filter']['spikein']['quantifier'] == 'unitas':
    UNITAS_REFSEQ.append(rules.spikein_unitas.output)
if config['filter']['contaminants']['quantifier'] == 'unitas':
    UNITAS_REFSEQ.append(rules.contaminants_unitas.output)
    
rule unitas_extra_reference:
    input:
        UNITAS_REFSEQ
    output:
        join(QUANT_INTERIM, 'unitas', 'refseq.fa')
    run:
        if len(input) > 0:
            shell('cat {input} > {output}')
        else:
            shell('touch {output}')
    
UNITAS_ARGS = ''
if not config['filter']['ribosomal']['filter'] and config['filter']['ribosomal']['quantifier'] == 'unitas':
    UNITAS_ARGS += '--riborase '

if len(UNITAS_REFSEQ) > 0:
    UNITAS_ARGS += '-refseq {} '.format(abspath(join(QUANT_INTERIM, 'unitas', 'refseq.fa')))


rule unitas_rundir:
    input:
        seqmap = rules.unitas_db.output.seqmap_exe,
        refdump = rules.unitas_db.output.refdump
    output:
        temp('seqmap.exe'),
        temp('UNITAS_refdump_{}'.format(ORG))
    shell:
        """
        cp {input.seqmap} {output.seqmap} 
        ln -sr {input.refdump} {output.refdump}
        """
        
rule unitas:
    input:
        fastq = get_filtered_fastq,
        refseq = rules.unitas_extra_reference.output,
        db = rules.unitas_db.output
    params:
        rundir = join(EXT_DIR, 'unitas'),
        outdir = abspath(join(QUANT_PROCESSED, 'unitas', '{sample}')),
        args = UNITAS_ARGS + ' -species_miR_only -keep_temp ',
        org  = config.get('organism', 'homo_sapiens')
    output:
        html = join(QUANT_PROCESSED, 'unitas', '{sample}', 'results.html'),
        trftable = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.tRF-table.simplified.txt'),
        annotation = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.annotation_summary.txt'),
        full_annotation = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.full_annotation_matrix.txt'),
        target_hits = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.hits_per_target.txt'),
        modifications = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.miR-modifications_{}.txt'.format(UNITAS_ORG)),
        trf_table_ext = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.tRF-table.txt'),
        mirtable = join(QUANT_PROCESSED, 'unitas', '{sample}', 'miR-table_{}.simplified.txt'.format(UNITAS_ORG)),
        pirna = join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.piRcandidates-tails_{}.txt'.format(UNITAS_ORG)),
        mirtable_ext= join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.miR-table_{}.txt'.format(UNITAS_ORG))
    singularity:
        'docker://' + config['docker']['unitas']
    threads:
        2
    shell:
        """
        mkdir -p {params.rundir}
        WORKDIR=$(pwd)
        cd {params.rundir} || exit
        unitas.pl -input $WORKDIR/{input.fastq} -s {params.org} -threads {threads} {params.args}
        mv UNITAS_*_{wildcards.sample}.*fastq_#1/* {params.outdir}/
        rm -rf UNITAS_*_{wildcards.sample}.*fastq_#1
        """

rule unitas_annotation2yaml:
    input:
        rules.unitas.output.annotation
    params:
        script = srcdir('scripts/unitas_annotation2json.py')
    output:
        join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.annotation_summary.json')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_mirtable:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'miR-table_{}.simplified.txt'.format(UNITAS_ORG)), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_mirtable.py')
    output:
        counts = join(QUANT_PROCESSED, 'unitas', 'unitas.mir.counts')
    shell:
        'python {params.script} {input} > {output}'
        
rule unitas_isomirtable:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.tRF-table.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_isomirtable.py')
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.isomir.counts')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_trftable:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.tRF-table.simplified.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_trftable.py')
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.trf.counts')
    shell:
        'python {params.script} {input} > {output}'
        
rule unitas_isotrftable:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.tRF-table.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_isotrftable.py')
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.isotrf.counts')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_allhits:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.hits_per_target.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_allhits.py')
        
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.allhits.counts')
    shell:
        'python {params.script} {input} --output {output}'
        
rule unitas_modifications:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.miR-modifications_{}.txt'.format(UNITAS_ORG)), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_modifications.py'),
        out = join(QUANT_PROCESSED, 'unitas', 'unitas.')
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.modification_per_position.counts'),
        join(QUANT_PROCESSED, 'unitas', 'unitas.internal_modification.counts')
    shell:
        'python {params.script} -o {params.out} {input}'
        
rule unitas_annotations:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.annotation_summary.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_annotations.py')
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.annotations.txt')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_allfeatures:
    input:
        expand(join(QUANT_PROCESSED, 'unitas', '{sample}', 'unitas.full_annotation_matrix.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_feature_counts.py')
        
    output:
        join(QUANT_PROCESSED, 'unitas', 'unitas.featurecounts.counts')
    shell:
        'python {params.script} --frac-counts {input} --output {output}'
        
rule unitas_all:
    input:
        rules.unitas_mirtable.output,
        rules.unitas_trftable.output,
        rules.unitas_isomirtable.output,
        rules.unitas_isotrftable.output,
        rules.unitas_allhits.output,
        rules.unitas_modifications.output,
        rules.unitas_annotations.output
