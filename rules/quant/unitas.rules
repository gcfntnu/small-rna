#-*- mode:snakemake -*-

from os.path import join, abspath
import warnings
warnings.filterwarnings("ignore", message="numpy.dtype size changed")

include:
    join(GCFDB_DIR, 'unitas.db')


rule unitas_mirtrace_fasta:
    input:
        join(FILTER_INTERIM, 'mirtrace', 'qc_passed_reads.all.collapsed', '{sample}_R1.fasta')
    output:
        join(FILTER_INTERIM, 'mirtrace', 'unitas_fasta', '{sample}_R1.fasta')
    shell:
        """
        sed -r '/>/ s/(.*)x([[:digit:]]+)(.*)/>\\2/' {input} > {output}
        """

def get_unitas_seq(wildcards):
    if config['filter']['trim']['quantifier'] == 'mirtrace':
        fastq = join(FILTER_INTERIM, 'mirtrace', 'unitas_fasta', '{sample}_R1.fasta')
    else:
        fastq = get_processed_fastq(wildcards)
    return fastq


rule spikein_unitas:
    input:
        get_spikein_fasta()
    params:
        script = srcdir('scripts/spikein_unitas.py')
    output:
        join(QUANT_INTERIM, 'unitas', '_spikein_unitas_formatted.fa')
    shell:
        'python {params.script} {input} > {output}'
        
UNITAS_REFSEQ = []
if config['filter']['spikein']['quantifier'] == 'unitas':
    UNITAS_REFSEQ.append(rules.spikein_unitas.output)
if config['filter']['contaminants']['quantifier'] == 'unitas':
    UNITAS_REFSEQ.append(rules.contaminants_unitas.output)
    
rule unitas_extra_reference:
    input:
        UNITAS_REFSEQ
    output:
        join(QUANT_INTERIM, 'unitas', 'refseq.fa')
    run:
        if len(input) > 0:
            shell('cat {input} > {output}')
        else:
            shell('touch {output}')
    
UNITAS_ARGS = ''
if config['filter']['ribosomal']['quantifier'] == 'unitas':
    UNITAS_ARGS += '--riborase '

if len(UNITAS_REFSEQ) > 0:
    UNITAS_ARGS += '-refseq {} '.format(abspath(join(QUANT_INTERIM, 'unitas', 'refseq.fa')))


rule unitas_rundir:
    input:
        seqmap = rules.unitas_db.output.seqmap_exe
    output:
        seqmap = 'seqmap.exe'
    params:
        outdir = rules.unitas_db.output.db
    shell:
        """
        cp {input.seqmap} .
        ln -srf {params.outdir} .
        """
        
rule unitas:
    input:
        seqmap = 'seqmap.exe',
        fastq = get_unitas_seq,
        refseq = rules.unitas_extra_reference.output
    params:
        outdir = abspath(join(QUANT_INTERIM, 'unitas', '{sample}')),
        args = UNITAS_ARGS + ' -species_miR_only ',
        org  = config['organism']
    output:
        html = join(QUANT_INTERIM, 'unitas', '{sample}', 'results.html'),
        trftable = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.tRF-table.simplified.txt'),
        annotation = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.annotation_summary.txt'),
        full_annotation = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.full_annotation_matrix.txt'),
        target_hits = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.hits_per_target.txt'),
        modifications = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.miR-modifications_{}.txt'.format(UNITAS_ORG)),
        trf_table_ext = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.tRF-table.txt'),
        mirtable = join(QUANT_INTERIM, 'unitas', '{sample}', 'miR-table_{}.simplified.txt'.format(UNITAS_ORG)),
        pirna = join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.piRcandidates-tails_{}.txt'.format(UNITAS_ORG)),
        mirtable_ext= join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.miR-table_{}.txt'.format(UNITAS_ORG))
    singularity:
        'docker://' + config['docker']['unitas']
    threads:
        2
    shell:
        """
        unitas.pl -input {input.fastq} -s {params.org} -threads {threads} {params.args}
        mv -f UNITAS_*{wildcards.sample}_R1.fast*_#1/* {params.outdir}/
        rm -rf UNITAS_*{wildcards.sample}_R1.*fas*_#1
        """

rule unitas_annotation2yaml:
    input:
        rules.unitas.output.annotation
    params:
        script = srcdir('scripts/unitas_annotation2json.py')
    output:
        join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.annotation_summary.json')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_mirtable:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'miR-table_{}.simplified.txt'.format(UNITAS_ORG)), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_mirtable.py')
    output:
        counts = join(QUANT_INTERIM, 'unitas', 'unitas.mir.counts')
    shell:
        'python {params.script} {input} > {output}'
        
rule unitas_isomirtable:
    input:
        expand(rules.unitas.output.mirtable_ext, sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_isomirtable.py')
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.isomir.counts')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_trftable:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.tRF-table.simplified.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_trftable.py')
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.trf.counts')
    shell:
        'python {params.script} {input} > {output}'
        
rule unitas_isotrftable:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.tRF-table.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_isotrftable.py')
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.isotrf.counts')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_allhits:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.hits_per_target.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_allhits.py')
        
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.allhits.counts')
    shell:
        'python {params.script} {input} --output {output}'
        
rule unitas_modifications:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.miR-modifications_{}.txt'.format(UNITAS_ORG)), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_modifications.py'),
        out = join(QUANT_INTERIM, 'unitas', 'unitas.')
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.modification_per_position.counts'),
        join(QUANT_INTERIM, 'unitas', 'unitas.internal_modification.counts')
    shell:
        'python {params.script} -o {params.out} {input}'
        
rule unitas_annotations:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.annotation_summary.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_annotations.py')
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.annotations.txt')
    shell:
        'python {params.script} {input} > {output}'

rule unitas_allfeatures:
    input:
        expand(join(QUANT_INTERIM, 'unitas', '{sample}', 'unitas.full_annotation_matrix.txt'), sample=SAMPLES)
    params:
        script = srcdir('scripts/unitas_feature_counts.py')
        
    output:
        join(QUANT_INTERIM, 'unitas', 'unitas.featurecounts.counts')
    shell:
        'python {params.script} --frac-counts {input} --output {output}'
        
rule unitas_all:
    input:
        rules.unitas_mirtable.output,
        rules.unitas_trftable.output,
        rules.unitas_isomirtable.output,
        rules.unitas_isotrftable.output,
        rules.unitas_allhits.output,
        rules.unitas_modifications.output,
        rules.unitas_annotations.output
