#-*- mode:snakemake -*-
from os.path import join

ADAPTER = config.get('adapter', 'TGGAATTCTCGG')

rule filter_adapter_atropos:
    input:
        fastq = _filter_get_raw
    output:
        trimmed = temp(join(FILTER_INTERIM, '{sample}', '{sample}.trimmed.atropos.fastq')),
        untrimmed = temp(join(FILTER_INTERIM, '{sample}', '{sample}.untrimmed.atropos.fastq'))
    params:
        args = '--mirna'
    singularity:
        'docker://flatberg/test:first-test'
    threads:
        4
    log:
        'logs/{sample}/atropos.log'
    shell:
        'atropos trim '
        '-se {input.fastq} '
        '--untrimmed-output {output.untrimmed} '
        '-o {output.trimmed} '
        '-T {threads} '
        '{params.args} '
        ' > {log}'

rule filter_bioscientific_trimmed:
    input:
        fastq = rules.filter_adapter_atropos.output.trimmed
    output:
        fastq = temp(join(FILTER_INTERIM, '{sample}', '{sample}.trimmed.bioscientific.1.fastq'))
    params:
        '-f 4 -t 4 -A -G --low_complexity_filter'
    threads:
        4
    conda:
        'envs/filter.yaml'
    log:
        html = 'logs/{sample}/fastp.trimmed.html',
        json = 'logs/{sample}/fastp.trimmed.json'
    shell:
        'fastp '
        '-i {input.fastq} '
        '-o {output.fastq} '
        '{params} ' 
        '--json {log.json} '
        '--html {log.html} '

rule filter_bioscientific_untrimmed:
    input:
        fastq = rules.filter_adapter_atropos.output.untrimmed
    output:
        fastq = temp(join(FILTER_INTERIM, '{sample}', '{sample}.trimmed.bioscientific.2.fastq'))
    params:
        '-f 4 -A -G --low_complexity_filter'
    threads:
        4
    conda:
        'envs/filter.yaml'
    log:
        html = 'logs/{sample}/fastp.untrimmed.html',
        json = 'logs/{sample}/fastp.untrimmed.json'
    shell:
        'fastp '
        '-i {input.fastq} '
        '-o {output.fastq} '
        '{params} ' 
        '--json {log.json} '
        '--html {log.html} '

rule filter_bioscientific:
    input:
        rules.filter_bioscientific_trimmed.output.fastq,
        rules.filter_bioscientific_untrimmed.output.fastq
    output:
        join(FILTER_INTERIM, '{sample}', '{sample}.trimmed.bioscientific.fastq')
    shell:
        'cat {input} > {output}'

rule filter_truseq:
    input:
        rules.filter_adapter_atropos.output
    output:
        join(FILTER_INTERIM, '{sample}', '{sample}.trimmed.truseq.fastq')
    params:
        '-A -G --low_complexity_filter'
    threads:
        4
    conda:
        'envs/filter.yaml'
    log:
        html = 'logs/{sample}/fastp.truseq.html',
        json = 'logs/{sample}/fastp.json'
    shell:
        'cat {input} | fastp --stdin '
        '-o {output} '
        '{params} ' 
        '--json {log.json} '
        '--html {log.html} '

def _filter_get_trimmed(wildcards):
    if not config['filter'].get('filter_trim', True):
        return _filter_get_raw(wildcards)
    if 'kit' in wildcards:
        kit = wildcards.kit
    else:
        kit = config.get('kit', 'bioscientific')
    if kit not in ['truseq', 'bioscientific']:
        raise ValueError('Kit argument: {} not supprted'.format(kit))
    return join(FILTER_INTERIM, wildcards.sample, '{}.trimmed.{}.fastq'.format(wildcards.sample, kit))
