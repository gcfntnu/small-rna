#-*- mode:snakemake -*-
import shutil
from os.path import join, abspath

include:
    'mirge.db'


rule mirge_symlink:
    input:
        get_filtered_fastq
    params:
        join(QUANT_INTERIM, 'mirge', 'fastq')
    output:
        join(QUANT_INTERIM, 'mirge', 'fastq', '{sample}.fastq')
    run:
        import os
        src = abspath(input[0])
        dst = abspath(output[0])
        os.symlink(src, dst)
        print('symlinking: {} > {}'.format(src, dst))

rule mirge_annotate:
    input:
        fastq = expand(rules.mirge_symlink.output, sample=SAMPLES),
        db = lambda wildcards: join(EXT_DIR, 'mirge', MIRGE_ORG[wildcards.org], 'annotation.Libs', 'human_trna.str') #fixme: need to work on other than human
    params:
        lib = join(EXT_DIR, 'mirge'),
        org = lambda wildcards: MIRGE_ORG[wildcards.org],
        args = '-di -ai -gff -ex 0 -trf',
        tmp_out = join(INTERIM_DIR, 'mirge','{org}','{db}'),
        out = join(QUANT_PROCESSED, 'mirge','{org}','{db}')
    output:
        mirtable = join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'miR.Counts.csv'),
        mirtable_ext = join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'isomirs.csv'),
        annotation = join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'annotation.report.csv'),
        umapped = join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'unmapped.csv'),
        a2i = join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'a2IEditing.report.newform.csv')
    singularity:
        'docker://flatberg/mirge:2.0.6'
    conda:
        'envs/mirge.yaml'
    threads:
        8
    shell:
        'mkdir -p {params.tmp_out} && '
        'miRge2.0 annotate '
        '-s {input.fastq} '
        '-d {wildcards.db} '
        '-lib {params.lib} '
        '-sp {params.org} '
        '{params.args} '
        '-cpu {threads} '
        '-o {params.tmp_out} '
        '&& mv {params.tmp_out}/miRge*/* {params.out} '
        '&& rm -rf {params.tmp_out} '

rule mirge_unmapped:
    input:
        join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'unmapped.csv')
    output:
        join(QUANT_PROCESSED, 'mirge','{org}','{db}', 'unmapped.fasta')
    run:
        with open(input[0]) as fh:
            txt = fh.read().splitlines()
        with open(output[0], 'w') as fh:
            for i, line in enumerate(txt[1:]):
                seq = line.split(',')[0]
                fh.write('>unmapped-{}\n'.format(i))
                fh.write(seq + '\n')
            
rule mirge_all:
    input:
        join(QUANT_PROCESSED, 'mirge', config['organism'], config['quant']['mirge']['db'], 'miR.Counts.csv'),
        join(QUANT_PROCESSED, 'mirge', config['organism'], config['quant']['mirge']['db'], 'unmapped.fasta')
    output:
        touch(join(QUANT_PROCESSED, 'mirge', config['organism'], config['quant']['mirge']['db'], '.done'))
