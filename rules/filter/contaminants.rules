#-*- mode:snakemake -*-
"""
http://seqanswers.com/forums/archive/index.php/t-42552.html
Those are common contaminant microbes that we encounter in sequencing
https://drive.google.com/file/d/0B3llHR93L14wZ1N6akxrSW16Z0U/view?usp=sharing
bbmap.sh in=ref.fa 
bbduk.sh -Xmx1g in=reads.fq out=clean.fq ref=phix.fasta k=31 hdist=1


NOTES:
emvec looks to be too diverse and perhaps only used when vector contamination is suspected
"""
from os.path import join


rule contaminants_phix:
    params:
        url = 'ftp://igenome:G3nom3s4u@ussd-ftp.illumina.com/PhiX/Illumina/RTA/PhiX_Illumina_RTA.tar.gz'
    output:
        join(EXT_DIR, 'contaminants', 'phix', 'fasta', 'phix.fa')
    shell:
        """
        curl {params.url} | tar xv
        mv PhiX/Illumina/RTA/Sequence/WholeGenomeFasta/genome.fa {output}
        rm -rf PhiX
        """

rule contaminants_bacterial:
    params:
        url = 'http://portal.nersc.gov/dna/microbial/assembly/bushnell/fusedEPmasked2.fa.gz',
    output:
        join(EXT_DIR, 'contaminants', 'bacterial_common_JGI', 'fasta', 'fusedEPmasked2.fa')
    shell:
        """wget {params.url}  | gunzip -c | sed 's/ /_/g' > {output}"""

rule contaminants_univec_fasta:
    params:
        url = 'http://ftp.ncbi.nlm.nih.gov/pub/UniVec/UniVec',
        url_core = 'http://ftp.ncbi.nlm.nih.gov/pub/UniVec/UniVec_Core'
    output:
        join(EXT_DIR, 'contaminants', 'univec', 'fasta', 'univec.fa')
    shell:
        'wget {params.url_core} -O- > {output}'

rule contaminants_univec_anno:
    input:
        rules.contaminants_univec_fasta.output
    output:
        join(EXT_DIR, 'contaminants', 'univec', 'anno.tsv')
    shell:
        """
        grep '>' {input} | sed 's/ /\t/' > {output}
        """
    
rule contaminants_emvec_fasta:
    """emvec processing from centrifuge code
    """
    params:
        url = 'http://ftp.ebi.ac.uk/pub/databases/emvec/emvec.dat.gz'
    output:
        join(EXT_DIR, 'contaminants', 'univec', 'fasta', 'emvec.fa')
    shell:
        """
        wget {params.url} -O- | gunzip -c | grep -E '^DE|^ ' | awk '/^DE/ {{ sub(/DE */,">"); gsub(/[ |]/,"_") }}; {{ print }}' | awk '/^ / {{ gsub(/ /,""); sub(/[0-9]*$/,"") }}; {{ print}}' > {output}
        """

rule contaminants_emvec_anno:
    input:
        rules.contaminants_emvec_fasta.output
    output:
        join(EXT_DIR, 'contaminants', 'emvec', 'anno.tsv')
    shell:
        """
        grep '>' {input} | sed 's/ /\t/' > {output}
        """

rule contaminants_fasta:
    input:
        rules.contaminants_univec_fasta.output
    output:
        join(EXT_DIR, 'contaminants', 'fasta', 'contaminants.fa')
    shell:
        'cat {input} > {output}'

rule contaminants_unitas:
    input:
        rules.contaminants_fasta.output
    params:
        script = srcdir('scripts/contaminants_unitas.py')
    output:
        temp(join(INTERIM_DIR, '_contaminants_unitas_formatted.fa'))
    shell:
        'python {params.script} {input} > {output}'

rule contaminants_index_bowtie:
    input:
        rules.contaminants_fasta.output
    params:
        index = join(EXT_DIR, 'contaminants', 'index', 'cont')
    output:
        join(EXT_DIR, 'contaminants', 'index', 'cont.1.ebwt'),
        join(EXT_DIR, 'contaminants', 'index', 'cont.2.ebwt'),
        join(EXT_DIR, 'contaminants', 'index', 'cont.3.ebwt'),
        join(EXT_DIR, 'contaminants', 'index', 'cont.4.ebwt'),
        join(EXT_DIR, 'contaminants', 'index', 'cont.rev.1.ebwt'),
        join(EXT_DIR, 'contaminants', 'index', 'cont.rev.2.ebwt'),
    conda:
        'envs/contaminants.yaml'
    singularity:
        'docker://flatberg/filter:0.1'
    shell:
        'bowtie-build {input} {params.index}'
        
rule contaminants_filter_bowtie:
    input:
        fastq = _filter_get_calibrator_clean,
        index = rules.contaminants_index_bowtie.output
    output:
        fastq = join(FILTER_INTERIM, '{sample}', '{sample}.contaminants.bowtie.clean.fastq'),
        count = join(FILTER_INTERIM, '{sample}', '{sample}.contaminants.bowtie.counts'),
        sam = temp(join(FILTER_INTERIM, '{sample}', '{sample}.contaminants.sam'))
    conda:
        'envs/contaminants.yaml'
    singularity:
        'docker://flatberg/filter:0.1'
    params:
        args = '-n 0 -k 1 -l 18 -q --best --norc -S ',
        index = rules.contaminants_index_bowtie.params.index
    threads:
        4
    log:
        bowtie = 'logs/{sample}/filter.contaminants.bowtie.log',
        error = 'logs/{sample}/contaminants.error'
    shell:
        'bowtie {params.index} '
        '{input.fastq} '
        '--un {output.fastq} '
        '-p {threads} '
        '{params.args} '
        '2>> {log.bowtie} '
        '| tee {output.sam} '
        '| samtools view -q 5 -S  - | cut -f3 | sort | uniq -c  > {output.count} '
        '2>> {log.error} '

rule contaminants_filter_bbduk:
    input:
        fastq = _filter_get_calibrator_clean,
        ref = 'data/ext/smallrna/contaminants/fasta/contaminants.fa'
    output:
        fastq = join(FILTER_INTERIM, '{sample}', '{sample}.contaminants.bbduk.clean.fastq'),
        count = join(FILTER_INTERIM, '{sample}', '{sample}.contaminants.bbduk.counts')
    params:
        args = 'rcomp=f k=27'
    conda:
        'envs/calibrators.yaml'
    singularity:
        'docker://flatberg/filter:0.1'
    log:
        'logs/{sample}/filter.contaminants.bbduk.log'
    threads:
        4
    shell:
        'bbduk.sh in={input.fastq} overwrite=true k=27 threads={threads} rcomp=f outu={output.fastq} stats={output.count} ref={input.ref} 2> {log}'

def _filter_get_contaminants_clean(wildcards):
    cnt = config['filter'].get('filter_contaminants', True)
    cnt_quant = config['filter'].get('contaminant_quantifier', 'bowtie')
    if cnt:
        if cnt_quant in ['bowtie', 'bbduk']:
            return join(FILTER_INTERIM, wildcards.sample, '{}.contaminants.{}.clean.fastq'.format(wildcards.sample, cnt_quant))
        elif cnt_quant == 'unitas':
            assert(config['filter'].get('quant') == 'unitas')
            return _filter_get_calibrator_clean(wildcards)
        else:
            raise ValueError('contaminant quantifier option not valid: {}'.format(quant))
    return _filter_get_calibrator_clean(wildcards)
