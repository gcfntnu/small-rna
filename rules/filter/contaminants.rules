#-*- mode:snakemake -*-
"""
NOTES:
"""
from os.path import join

CONT_DIR = join(EXT_DIR, 'contaminants')

def contaminants_get_fasta(*args, **kw):
    ref = config['filter']['contaminants']['ref'].split(',')
    out = []
    for r in ref:
        if r.lower() == 'univec':
            out.append(join(EXT_DIR, 'univec', 'fasta', 'univec.fa'))
        if r.lower() == 'emvec':
            out.append(join(EXT_DIR, 'emvec', 'fasta', 'emvec.fa'))
        if r.lower() == 'common_bacterial_euk':
            out.append(join(EXT_DIR, 'JGI', 'fasta', 'fusedEPmasked2.fa'))
        if r.lower() == 'common_bacterial_prok':
            out.append(join(EXT_DIR, 'JGI', 'fasta', 'fusedEPmasked2.fa'))
                       
    return out

rule contaminants_fasta:
    input:
        contaminants_get_fasta
    output:
        join(EXT_DIR, 'contaminants', 'fasta', 'contaminants.fa')
    shell:
        'cat {input} > {output}'

rule contaminants_unitas:
    input:
        rules.contaminants_fasta.output
    params:
        script = srcdir('scripts/contaminants_unitas.py')
    output:
        temp(join(INTERIM_DIR, '_contaminants_unitas_formatted.fa'))
    shell:
        'python {params.script} {input} > {output}'

rule contaminants_build_index_bowtie:
    input:
        rules.contaminants_fasta.output
    output:
        join(CONT_DIR, 'index', 'bowtie', 'contaminants.1.ebwt'),
        join(CONT_DIR, 'index', 'bowtie', 'contaminants.2.ebwt'),
        join(CONT_DIR, 'index', 'bowtie', 'contaminants.3.ebwt'),
        join(CONT_DIR, 'index', 'bowtie', 'contaminants.4.ebwt'),
        join(CONT_DIR, 'index', 'bowtie', 'contaminants.rev.1.ebwt'),
        join(CONT_DIR, 'index', 'bowtie', 'contaminants.rev.2.ebwt')
    params:
        index = join(CONT_DIR, 'index', 'bowtie', 'contaminants')
    singularity:
        'docker://flatberg/filter:0.1'
    shell:
        'bowtie-build {input} {params.index}'

rule contaminants_build_index_bowtie2:
    input:
        rules.contaminants_fasta.output
    output:
        join(CONT_DIR, 'index', 'bowtie2', 'contaminants.1.bt2'),
        join(CONT_DIR, 'index', 'bowtie2', 'contaminants.2.bt2'),
        join(CONT_DIR, 'index', 'bowtie2', 'contaminants.3.bt2'),
        join(CONT_DIR, 'index', 'bowtie2', 'contaminants.4.bt2'),
        join(CONT_DIR, 'index', 'bowtie2', 'contaminants.rev.1.bt2'),
        join(CONT_DIR, 'index', 'bowtie2', 'contaminants.rev.2.bt2')
    params:
        index = join(CONT_DIR, 'index', 'bowtie2', 'contaminants')
    singularity:
        'docker://flatberg/filter:0.1'
    shell:
        'bowtie2-build {input} {params.index}'

rule contaminants_filter_se_bowtie:
    input:
        R1 = join(FILTER_INTERIM, '{merge}', 'spikein', config['filter']['spikein']['quantifier'], '{sample}_R1.fastq'),
        index = rules.contaminants_build_index_bowtie.output
    output:
        R1 = join(FILTER_INTERIM, '{merge}', 'contaminants', 'bowtie', '{sample}_R1.fastq'),
        counts = join(FILTER_INTERIM, '{merge}', 'contaminants', 'bowtie', '{sample}_R1.counts')
    singularity:
        'docker://flatberg/filter:0.1'
    params:
        args = '-n 0 -k 1 -l 18 -q --best --norc -S ',
        index = rules.contaminants_build_index_bowtie.params.index
    threads:
        2
    log:
        bowtie = 'logs/{sample}/filter.{merge}.contaminants.bowtie.log',
        error = 'logs/{sample}/filter.{merge}.contaminants.bowtie.error'
    shell:
        'bowtie {params.index} '
        '{input.R1} '
        '--un {output.R1} '
        '-p {threads} '
        '{params.args} '
        '2>> {log.bowtie} '
        '| samtools view -q 5 -S - | cut -f3 | sort | uniq -c  > {output.counts} '
        '2>> {log.error} '
        
rule contaminants_filter_se_bowtie2:
    input:
        R1 = join(FILTER_INTERIM, '{merge}', 'spikein', config['filter']['spikein']['quantifier'], '{sample}_R1.fastq'),
        index = rules.contaminants_build_index_bowtie2.output
    output:
        R1 = join(FILTER_INTERIM, '{merge}', 'contaminants', 'bowtie2', '{sample}_R1.fastq'),
        counts = join(FILTER_INTERIM, '{merge}', 'contaminants', 'bowtie2', '{sample}_R1.counts')
    params:
        args = '-D 15 -R 2 -N 0 -L 18 -i S,1,0 --norc',
        index = rules.contaminants_build_index_bowtie2.params.index
    threads:
        2
    log:
        bowtie = 'logs/{sample}/filter.{merge}.contaminants.bowtie2.log',
        error = 'logs/{sample}/filter.{merge}.error'
    shell:
        'bowtie2 '
        '-U {input.R1} '
        '--un {output.R1} '
        '-x {params.index} '
        '-p {threads} '
        '{params.args} '
        '2>> {log.bowtie} '
        '| samtools view -S -q5 - | cut -f3 | sort | uniq -c  > {output.counts} '
        '2>> {log.error} '

rule contaminants_build_index_bbmap:
    input:
         rules.contaminants_fasta.output
    output:
        join(CONT_DIR, 'index', 'bbmap', 'ref', 'genome', '1', 'info.txt')
    params:
        index = join(CONT_DIR, 'index', 'bbmap')
    conda:
        'envs/contaminants.yaml'
    singularity:
        'docker://quay.io/biocontainers/bbmap'
    shell:
        'bbmap.sh ref={input} path={params.index}'

rule contaminants_filter_se_bbduk:
    input:
        R1 = join(FILTER_INTERIM, '{merge}', 'spikein', config['filter']['spikein']['quantifier'], '{sample}_R1.fastq'),
        ref = rules.contaminants_fasta.output
    output:
        R1 = join(FILTER_INTERIM, '{merge}', 'contaminants', 'bbduk', '{sample}_R1.fastq'),
        counts = join(FILTER_INTERIM, '{merge}', 'contaminants', 'bbduk', '{sample}_R1.counts')
    singularity:
        'docker://quay.io/biocontainers/bbmap'
    threads:
        4
    log:
        'logs/{sample}/filter.{merge}.contaminants.bbduk.log'
    shell:
        'bbduk.sh in={input.R1} overwrite=true outu={output.R1} threads={threads} rcomp=f k=21 stats={output.counts} ref={input.ref} 2>{log}'

        
rule contaminants_filter_skip:
    input:
        join(FILTER_INTERIM, '{merge}', 'spikein', config['filter']['spikein']['quantifier'], '{sample}_R{read_num}.fastq')
    output:
        join(FILTER_INTERIM, '{merge}', 'contaminants', 'skip', '{sample}_R{read_num}.fastq')
    shell:
        'ln -sr {input} {output}'
